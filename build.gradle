plugins {
    id 'java'
    id 'application'
}

group = 'io.github.seiya_matsuoka'
version = '0.1.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'io.github.seiya_matsuoka.interactivelogmaskingcli.App'
    applicationName = 'interactive-log-masking-cli'
    applicationDefaultJvmArgs = ['-Dfile.encoding=UTF-8']
}

dependencies {
    // JSON (Jackson)
    implementation(platform('com.fasterxml.jackson:jackson-bom:2.17.2'))
    implementation('com.fasterxml.jackson.core:jackson-databind')

    // Test (JUnit 5)
    testImplementation(platform('org.junit:junit-bom:5.10.3'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

tasks.named('run', JavaExec) {
    standardInput = System.in
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.test {
    useJUnitPlatform()
}

distributions {
    main {
        contents {
            // 配布物に rules/ を同梱
            from('rules') {
                into('rules')
            }
            // 配布物に input/ を同梱（サンプルログ一式）
            from('input') {
                into('input')
            }
            from('out') {
                into('out')
                include('.gitkeep')
            }
        }
    }
}

/**
 * GroovyLint 設定（VS Code: Groovy Lint, Format and Fix）について
 *
 * - build.gradle / settings.gradle は Gradle DSL のため動的な記述が多く、CompileStatic 系ルールが誤検知になりやすい。
 *   そのため .groovylintrc.json を作成し CompileStatic ルールをプロジェクト全体で無効化すること。
 */
